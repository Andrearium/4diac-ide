/*******************************************************************************
 * Copyright (c) 2015, 2017 fortiss GmbH
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *   Martin Jobst, Monika Wenger 
 *       - initial API and implementation and/or initial documentation
 *******************************************************************************/
module org.eclipse.fordiac.ide.model.structuredtext.GenerateStructuredText

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.fordiac.ide.model.structuredtext"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
			    genericIde = {
			        name = "org.eclipse.fordiac.ide.model.structuredtext.ui"
			    }
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
	    language = XtextGeneratorLanguage {
			name = "org.eclipse.fordiac.ide.model.structuredtext.StructuredText"
			fileExtensions = "st"
	        referencedResource = "platform:/resource/org.eclipse.fordiac.ide.model/model/fordiac.genmodel"
	     
	        fragment = grammarAccess.GrammarAccessFragment2 {}
	        fragment = ecore.EMFGeneratorFragment2 {}
	        fragment = resourceFactory.ResourceFactoryFragment2 {}
	        fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
	        	options = {
					ignoreCase = true
				}
	        }
	        fragment = exporting.QualifiedNamesFragment2 {}
	        fragment = ui.labeling.LabelProviderFragment2 {}
	        fragment = ui.outline.QuickOutlineFragment2 {}
	        fragment = ui.outline.OutlineTreeProviderFragment2 {
	        	generateStub = false
	        }
	        fragment = ui.quickfix.QuickfixProviderFragment2 {
	        	generateStub = false
	        }
	        fragment = ui.contentAssist.ContentAssistFragment2 {
	        	generateStub = false
	        }
	        fragment = ui.refactoring.RefactorElementNameFragment2 {}
	        fragment = ui.templates.CodetemplatesGeneratorFragment2 {}
	        fragment = ui.compare.CompareFragment2 {}
	    }
	}
}
