/*******************************************************************************
 * Copyright (c) 2020 Sandor Bacsi
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *   Sandor Bacsi - initial API and implementation and/or initial documentation
 *******************************************************************************/
import 'platform:/resource/org.eclipse.fordiac.ide.model/model/lib.ecore'

package libraryElement

-- A self-cycle exists when there is a transition from an EC state back to itself. Self-cycles must be gated by events.
-- Problem: The process that is executing the ECC may enter an infinite loop
context ECTransition
inv SelfCycleMustBeGatedByEvent: self.source = self.destination implies (not self.conditionEvent.oclIsUndefined())

-- Transitions with lower priority than the 1 transition condition will never be taken regardless they are with pure data-driven conditions or event conditions
-- Problem: Dead transitions
context ECTransition
inv TransitionLowerThanOne: not self.conditionEvent.oclIsUndefined() or self.conditionExpression <> '1' implies (self.source.outTransitions->forAll(trans | trans.conditionExpression = '1' implies (trans.getPriority() > self.getPriority())))

-- Transitions with higher priority than the 1 transition condition can only be taken if they are with pure data-driven conditions
-- Problem: Dead transitions
context ECTransition
inv TransitionHigherThanOne: self.conditionExpression <> '1' implies self.source.outTransitions->forAll(trans | (trans.conditionExpression = '1' and trans.getPriority() > self.getPriority()) implies (self.conditionEvent.oclIsUndefined()))

-- States (except start state) which do not have any input transitions
-- Problem: Unreachable states
context ECState
inv UnreachableState: not self.isStartState() implies(self.inTransitions->size() > 0)

-- States which do not have any outgoing EC transitions
-- Problem: Terminal states
context ECState
inv TerminalState: self.outTransitions->size() > 0

-- States (except start state) to which a path cannot be found from the EC initial state by following the directed links
-- Problem: Dead states
context ECState
inv DeadState : not self.isStartState() implies (self->closure(inTransitions.source)->select(state : ECState | state.isStartState())->size() > 0)

-- Event inputs of the FB type containing the ECC that are not used in any EC transitions
context ECC
inv UnusedEventInput: self.eCTransition.conditionEvent->includesAll(self.basicFBType.interfaceList.eventInputs->asSet())

-- Event outputs of the FB type containing the ECC that are not used in any EC actions
context ECC
inv UnusedEventOutput: self.eCTransition.destination.eCAction.output->includesAll(self.basicFBType.interfaceList.eventOutputs->asSet())

-- Transition conditions as "None" without any condition (empty or spaces) are invalid transitions
context ECTransition
inv NoneTransitionWithoutCondition: self.conditionEvent.oclIsUndefined() implies(self.conditionExpression <> '' and Sequence{1..self.conditionExpression.size()}->select(i | self.conditionExpression.substring(i, i) = ' ')->size() <> self.conditionExpression.size() )

-- A guard-only cycle is the general case of a self-cycle (SelfCycleMustBeGatedByEvent), where a path exists from an EC state, through other EC states and back to itself through a path consisting of transitions with guard-only conditions (i.e., transitions not containing an event).
-- Problem: The process that is executing the ECC may enter an infinite loop
-- TODO: Checking guard-only conditions (It can only detect simple cycles now.)
-- context ECState
-- inv GuardOnlyCycle : not self->closure(outTransitions.destination)->includes(self)

endpackage