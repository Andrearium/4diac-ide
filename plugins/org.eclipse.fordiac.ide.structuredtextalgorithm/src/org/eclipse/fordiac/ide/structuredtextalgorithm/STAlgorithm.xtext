/*******************************************************************************
 * Copyright (c) 2022 Martin Erich Jobst
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Martin Jobst - initial API and implementation and/or initial documentation
 *******************************************************************************/
grammar org.eclipse.fordiac.ide.structuredtextalgorithm.STAlgorithm with org.eclipse.fordiac.ide.structuredtextcore.STCore

import "http://www.eclipse.org/fordiac/ide/structuredtextalgorithm/STAlgorithm"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "org.eclipse.fordiac.ide.model.datatype" as datatype
import "org.eclipse.fordiac.ide.model.libraryElement" as libraryElement
import "http://www.eclipse.org/fordiac/ide/structuredtextcore/STCore" as stcore

STAlgorithmSource returns stcore::STSource:
	{STAlgorithmSource} elements+=STAlgorithmSourceElement*;

STAlgorithmSourceElement returns STAlgorithmSourceElement:
	STAlgorithm | STMethod;

STAlgorithm:
	'ALGORITHM' name=ID
	body=STAlgorithmBody
	'END_ALGORITHM';

STAlgorithmBody:
	{STAlgorithmBody}
	varTempDeclarations+=STVarTempDeclarationBlock*
	statements+=STStatement*;

STMethod:
	'METHOD' name=ID (':' returnType=[datatype::DataType|STAnyType])?
	body=STMethodBody
	'END_METHOD';

STMethodBody:
	{STMethodBody}
	varDeclarations+=(STVarTempDeclarationBlock | STVarInputDeclarationBlock | STVarOutputDeclarationBlock | STVarInOutDeclarationBlock)*
	statements+=STStatement*;
